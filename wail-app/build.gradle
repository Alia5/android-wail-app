apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 20
    buildToolsVersion '20.0.0'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 20
        versionCode 22
        versionName '0.6.4 beta'
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            versionNameSuffix ' debug ' + getDateForVersionName()
        }

        release {
            signingConfig signingConfigs.release
        }
    }

    if (project.hasProperty('signingConfig.storeFile')
        && project.hasProperty('signingConfig.storePassword')
        && project.hasProperty('signingConfig.keyAlias')
        && project.hasProperty('signingConfig.keyPassword')
    ) {
        android.signingConfigs.release.storeFile     = file(project.getProperty('signingConfig.storeFile'))
        android.signingConfigs.release.storePassword = project.getProperty('signingConfig.storePassword')
        android.signingConfigs.release.keyAlias      = project.getProperty('signingConfig.keyAlias')
        android.signingConfigs.release.keyPassword   = project.getProperty('signingConfig.keyPassword')
    } else {
        buildTypes.release.signingConfig = null
    }
}

dependencies {
    // NOTICE, libraries versions are FIXED for stability
    // because gradle can automatically load newer version which can add bugs and have to be tested

    compile 'com.android.support:support-v13:20.0.0'
    compile 'com.github.chrisbanes.actionbarpulltorefresh:library:0.9.3'

    compile fileTree(dir: 'libs', include: '*.jar')
}

def getDateForVersionName() {
    return new Date().format('yyyy.MM.dd HH:mm:ss');
}
