// for applying custom environment properties
if (new File('wail-app/custom_env.gradle').exists()) {
    apply from: 'custom_env.gradle'
}

apply plugin: 'com.android.application'

repositories {
    maven() {
        url 'http://jcenter.bintray.com'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion '20.0.0'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 26
        versionName '0.7.2 beta'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    lintOptions {
        disable 'InvalidPackage'
    }

    signingConfigs {
        googlePlay
    }

    buildTypes {
        debug {
            versionNameSuffix ' debug ' + getDateForVersionName()
            signingConfig signingConfigs.googlePlay
        }

        release {
            signingConfig signingConfigs.googlePlay
        }
    }

    if (project.hasProperty('signingConfigStoreFile')
            && project.hasProperty('signingConfigStorePassword')
            && project.hasProperty('signingConfigKeyAlias')
            && project.hasProperty('signingConfigKeyPassword')
    ) {
        android.signingConfigs.googlePlay.storeFile = file(project.ext['signingConfigStoreFile'])
        android.signingConfigs.googlePlay.storePassword = project.ext['signingConfigStorePassword']
        android.signingConfigs.googlePlay.keyAlias = project.ext['signingConfigKeyAlias']
        android.signingConfigs.googlePlay.keyPassword = project.ext['signingConfigKeyPassword']
    } else {
        buildTypes.all { bt ->
            bt.signingConfig = null
        }
    }
}

dependencies {
    // NOTICE, libraries versions are FIXED for stability
    // because gradle can automatically load newer version which can add bugs and have to be tested

    compile 'com.android.support:support-v13:20.0.0'
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:cardview-v7:21.0.3'
    compile 'com.melnykov:floatingactionbutton:1.1.0'
    compile 'com.rengwuxian.materialedittext:library:1.7.1'
    compile 'com.afollestad:material-dialogs:0.6.2.1'
    compile 'com.jakewharton:butterknife:5.1.2'

    compile fileTree(dir: 'libs', include: '*.jar')
}

def getDateForVersionName() {
    return new Date().format('yyyy.MM.dd HH:mm:ss');
}
